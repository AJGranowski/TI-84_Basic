// MULTI-PAGE MENU TEMPLATE
// X = Current option
// Y = Current page
// A = Number of options
// B = Number of pages
// |u = Expression for option positions
1→X
1→Y
3→A
2→B
"X→|u

// Menu loop
Repeat Ans
	// STATIC CONTENT DRAW
	ClrHome
	Y
	
	// Page 1 static content
	While Ans=1
		Output(1,2,"A
		Output(2,2,"B
		Output(3,2,"C
		Output(1,10,"MENU 1
		0
	End
	
	// Page 2 static content
	While Ans=2
		Output(1,2,"D
		Output(2,2,"E
		Output(1,10,"MENU 2
		0
	End
	
	// Initialize the selector
	Output(|u,1,">
	
	// Wait for page change or enter
	Repeat Ans=105 or Ans=24 or Ans=26
		// ON KEY PRESS DRAW
		
		// Wait until a key is pressed
		Repeat Ans
			// ON TICK DRAW
			getKey
		End
		
		// If the key was up or down, adjust the cursor
		While Ans=25 or Ans=34
			
			Output(|u,1," 
			
			X+(Ans=34)-(Ans=25→X
			If Ans>A
				1→X
			If not(Ans
				A→X
			Output(|u,1,">
			
			0
		End
	End
	// ON ACTION DRAW
	
	// If the key was left or right, adjust the page
	While Ans=24 or Ans=26
		Y+(Ans=26)-(Ans=24→Y
		If Ans>B
			1→Y
		If not(Ans
			B→Y
		If Ans=1
			3→A
		If Y=2
			2→A
		1→X
		0
	End
	
	// If the key was enter
	While Ans=105
		// ON ENTER DRAW
		
		// Do special stuff if this option lets you
		X=2 and Y=1
		While Ans
			Disp "
			Input ">B=",Str1
			DelVar Str1
			0
		End
		
		not(X=2 and Y=1
	End

End

ClrHome

Disp "YOU SELECTED "+sub("ABCDE",X+3(Y-1),1
Pause 
ClrHome
DelVar |uDelVar ADelVar BDelVar Y